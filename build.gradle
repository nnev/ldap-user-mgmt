plugins {
  id "java"
  id "application"

  id "com.github.ben-manes.versions" version "0.53.0"
}

group = "de.nnev"
version = "1.0-SNAPSHOT"

application {
  mainClass.set("de.nnev.mgmt.Manager")
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation("info.picocli:picocli:4.7.7")
  annotationProcessor("info.picocli:picocli-codegen:4.7.7")

  implementation("com.unboundid:unboundid-ldapsdk:7.0.4")
  implementation("com.kohlschutter.junixsocket:junixsocket-core:2.10.1")

  testImplementation(platform("org.junit:junit-bom:6.0.3"))
  testImplementation("org.junit.jupiter:junit-jupiter")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

compileJava {
  options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

tasks.named("test", Test) {
  useJUnitPlatform()
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
dependencyUpdates.gradleReleaseChannel="current"
